load("@npm//@bazel/jasmine:index.bzl", "jasmine_node_test")
load("//tools:defaults.bzl", "pkg_npm", "ts_library")
load("//tools:ts_json_schema.bzl", "ts_json_schema")

# Copyright Google Inc. All Rights Reserved.
#
# Use of this source code is governed by an MIT-style license that can be
# found in the LICENSE file at https://angular.io/license
licenses(["notice"])

package(default_visibility = ["//visibility:public"])

# @angular-devkit/schematics-cli
ts_library(
    name = "schematics_cli",
    package_name = "@angular-devkit/schematics-cli",
    srcs = glob(
        include = ["**/*.ts"],
        exclude = [
            "**/*_spec.ts",
            "schematic/files/**",
            "blank/project-files/**",
            "blank/schematic-files/**",
            # NB: we need to exclude the nested node_modules that is laid out by yarn workspaces
            "node_modules/**",
        ],
    ) + [
        # @external_begin
        # These files are generated from the JSON schema
        "//packages/angular_devkit/schematics_cli:blank/schema.ts",
        "//packages/angular_devkit/schematics_cli:schematic/schema.ts",
        # @external_end
    ],
    data = glob(
        include = [
            "collection.json",
            "package.json",
            "blank/schema.json",
            "blank/project-files/**/*",
            "blank/schematic-files/**/*",
            "schematic/schema.json",
            "schematic/files/**/*",
        ],
    ),
    module_name = "@angular-devkit/schematics-cli",
    module_root = "bin/schematics.d.ts",
    deps = [
        "//packages/angular_devkit/core",
        "//packages/angular_devkit/core/node",
        "//packages/angular_devkit/schematics",
        "//packages/angular_devkit/schematics/tasks",
        "//packages/angular_devkit/schematics/tools",
        "@npm//@types/inquirer",
        "@npm//@types/node",
        "@npm//@types/yargs-parser",
        "@npm//ansi-colors",
        "@npm//inquirer",  # @external
        "@npm//symbol-observable",  # @external
        "@npm//yargs-parser",  # @external
    ],
)

ts_library(
    name = "schematics_cli_test_lib",
    testonly = True,
    srcs = glob(
        include = [
            "bin/**/*_spec.ts",
        ],
    ),
    # strict_checks = False,
    deps = [
        ":schematics_cli",
    ],
)

jasmine_node_test(
    name = "schematics_cli_test",
    srcs = [":schematics_cli_test_lib"],
)

ts_json_schema(
    name = "blank_schema",
    src = "blank/schema.json",
)

ts_json_schema(
    name = "schematic_schema",
    src = "schematic/schema.json",
)

genrule(
    name = "license",
    srcs = ["//:LICENSE"],
    outs = ["LICENSE"],
    cmd = "cp $(execpath //:LICENSE) $@",
)

pkg_npm(
    name = "npm_package",
    pkg_deps = [
        "//packages/angular_devkit/schematics:package.json",
        "//packages/angular_devkit/core:package.json",
    ],
    deps = [
        ":README.md",
        ":license",
        ":schematics_cli",
    ],
)
